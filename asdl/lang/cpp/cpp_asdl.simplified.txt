## ASDL's 4 builtin types are
identifier, int, string, constant

cu = TranslationUnit(statement* stmts)

declaration = AccessSpecDecl(access_spec access_spec)
            | CXXConstructorDecl(identifier name, constant? constexpr, exception_spec? exception, default_mode? defaulted, statement? body, attribute* attributes, parameter* parameters, cxx_ctor_initializer* initializers, constant? explicit)
            | CXXConversionDecl(identifier name, constant? inline, exception_spec? exception, constant? const, statement? body, attribute* attributes)
            | CXXDestructorDecl(identifier name, exception_spec? exception, virtual_spec? virtual, default_mode? defaulted, statement? body, attribute* attributes)
            | CXXMethodDecl(identifier name, constant? constexpr, type return_type, constant? storage, constant? variadic, constant? inline, constant? trailing_return, exception_spec? exception, virtual_spec? virtual, constant? const, default_mode? defaulted, method_attr* method_attributes, constant? ref_qualifier, statement? body, attribute* attributes, parameter* parameters)
            | CXXRecordDecl(identifier name, identifier kind, base* bases, constant complete, declaration* decls)
            | ClassTemplateSpecializationDecl(template_argument* template_arguments, template_parameter* template_parameters, identifier name, identifier kind, base* bases, constant complete, declaration* decls)
            | ClassTemplatePartialSpecializationDecl(template_argument* template_arguments, template_parameter* template_parameters, identifier name, identifier kind, base* bases, constant complete, declaration* decls)
            | ClassTemplateDecl(template_parameter* template_parameters, declaration decl)
            | EmptyDecl
            | EnumDecl(identifier? name, enum_field* fields)
            | FieldDecl(identifier name, type type, expression? init, expression? bitwidth, constant? type_qualifier, attribute* attributes)
            | FriendDecl(declaration? decl, constant? raw_decl)
            | FunctionDecl(identifier name, constant? constexpr, type return_type, constant? storage, constant? variadic, constant? inline, constant? trailing_return, exception_spec? exception, statement? body, attribute* attributes, parameter* parameters)
            | FunctionTemplateDecl(template_parameter* template_parameters, declaration decl)
            | NamespaceDecl(identifier? name, constant? inline, declaration* decls)
            | StaticAssertDecl(expression cond, constant? message)
            | TypedefDecl(identifier name, type type)
            | TypeAliasTemplateDecl(template_parameter* template_parameters, declaration decl)
            | TypeAliasDecl(identifier name, type type)
            | UnresolvedUsingTypenameDecl(identifier name)
            | UsingDecl(identifier name)
            | UsingDirectiveDecl(identifier name)
            | VarDecl(identifier name, constant? constexpr, type type, constant? storage_class, constant init_mode, constant? implicit, constant? referenced, expression? init, attribute* attributes, constant? tls)

template_parameter = TemplateTemplateParmDecl(identifier? name, template_parameter* template_parameters)
                   | TemplateTypeParmDecl(identifier? name, template_tag tag, template_argument? default, constant? parameter_pack)
                   | NonTypeTemplateParmDecl(type type, identifier? name, template_argument? default, constant? parameter_pack)

template_tag = ClassTag | TypenameTag

template_argument = TemplateArgument(type? type, expression? expr, template_argument* pack)

statement = AttributedStmt(statement stmt, attribute* attributes)
          | BreakStmt
          | CXXForRangeStmt(declaration decl, expression range, statement body)
          | CXXTryStmt(statement body, exception_handler* handlers)
          | CaseStmt(expression pattern, expression? pattern_end, statement stmt)
          | CompoundStmt(statement* stmts)
          | ContinueStmt
          | DeclStmt(declaration* decls)
          | DefaultStmt(statement stmt)
          | DoStmt(expression cond, statement body)
          | ExprStmt(expression expr)
          | ForStmt(declarations_or_expression? init, declaration_or_expression? cond, expression? inc, statement body)
          | FullComment(string comment)
          | GCCAsmStmt(constant string, constrained_expression* output_operands, constrained_expression* input_operands, constant* clobbers, constant* labels)
          | GotoStmt(identifier target)
          | IfStmt(declaration_or_expression cond, statement true_body, statement? false_body)
          | IndirectGotoStmt(expression expr)
          | LabelStmt(identifier name, statement stmt)
          | NullStmt
          | OverrideAttr
          | ReturnStmt(expression? value)
          | SwitchStmt(expression cond, statement body)
          | TypeRef(identifier name)
          | WhileStmt(declaration_or_expression cond, statement body)

expression = AddrLabelExpr(identifier name)
           | ArraySubscriptExpr(expression base, expression index)
           | AtomicExpr(identifier name, expression* args)
           | BinaryConditionalOperator(expression cond, expression false_expr)
           | BinaryOperator(constant opcode, expression lhs, expression rhs)
           | BuiltinBitCastExpr(type type, expression expr)
           | ChooseExpr(expression cond, expression true_expr, expression false_expr)
           | CStyleCastExpr(type type, expression expr)
           | CXXBindTemporaryExpr(expression expr)
           | CXXBoolLiteralExpr(constant value)
           | CXXConstructExpr(expression* args)
           | CXXDefaultArgExpr
           | CXXDeleteExpr(expression expr, constant? is_array)
           | CXXFunctionalCastExpr(type type, expression expr)
           | CXXMemberCallExpr(expression bound_method, expression* args)
           | CXXNewExpr(type type, expression* args, expression? placement, expression? array_size, expression? placement)
           | CXXNullPtrLiteralExpr
           | CXXOperatorCallExpr(expression left, expression op, expression right)
           | CXXReinterpretCastExpr(type type, expression expr)
           | CXXStaticCastExpr(type type, expression expr, constant value_category)
           | CXXConstCastExpr(type type, expression expr, constant value_category)
           | CXXStdInitializerListExpr(expression* subnodes)
           | CXXTemporaryObjectExpr(type type, expression* args)
           | CXXThisExpr
           | CXXThrowExpr(expression? expr)
           | CXXTypeidExpr(expression? expr, type? type)
           | CXXUnresolvedConstructExpr(type type, expression expr)
           | CallExpr(expression callee, expression* args)
           | CharacterLiteral(identifier value)
           | CompoundAssignOperator(constant opcode, expression lhs, expression rhs)
           | ConditionalOperator(expression cond, expression true_expr, expression false_expr)
           | ConstantExpr(expression expr, identifier? result)
           | DeclRefExpr(identifier name)
           | DependentScopeDeclRefExpr(identifier name)
           | DumpedExpr(string value)
           | ExprWithCleanups(expression expr)
           | FloatingLiteral(type type, identifier value)
           | ImaginaryLiteral(type type, identifier value)
           | ImplicitCastExpr(type type, expression expr)
           | ImplicitValueInitExpr
           | InitListExpr(expression* values)
           | IntegerLiteral(type type, identifier value)
           | LambdaExpr(parameter* parameters, expression* capture_exprs, type? trailing_type, exception_spec? exception, constant? variadic, statement body, attribute* attributes)
           | MaterializeTemporaryExpr(expression expr)
           | MemberExpr(identifier name, identifier op, expression? expr)
           | OffsetOfExpr(type type, offsetof_kind* kinds)
           | OpaqueValueExpr(expression expr)
           | PackExpansionExpr(expression expr)
           | ParenExpr(expression expr)
           | ParenListExpr(expression* exprs)
           | PredefinedExpr(identifier name)
           | SizeOfPackExpr(identifier name)
           | StmtExpr(statement stmt)
           | StringLiteral(constant value)
           | SubstNonTypeTemplateParmExpr(declaration decl, expression expr)
           | UnaryExprOrTypeTraitExpr(identifier name, expression? expr, type? type)
           | UnaryOperator(constant opcode, constant postfix, expression expr)
           | UnresolvedLookupExpr(identifier name)
           | UserDefinedLiteral(constant suffix, expression expr)
           | VAArgExpr(expression expr, type type)
           | CXXDefaultInitExpr(expression expr)

parameter = ParmVarDecl(type type, identifier? name, expression? default, attribute* attributes)

declarations_or_expression = DeclsOrExpr(declaration* decls, expression? expr)

declaration_or_expression = DeclOrExpr(declaration? decl, expression? expr)

cxx_ctor_initializer = CXXCtorInitializer(identifier name, expression* args)


type = AutoType(auto_keyword keyword)
     | BitIntType(constant size, constant sign)
     | BuiltinType(identifier name)
     | ComplexType(type type)
     | ConstantArrayType(type type, constant size)
     | DecayedType(type type)
     | DecltypeType(constant repr)
     | DependentNameType(constant nested, constant attr)
     | DependentSizedArrayType(type type, constant? size_repr)
     | ElaboratedType(type type, identifier? qualifiers)
     | EnumType(identifier name)
     | FunctionNoProtoType
     | FunctionProtoType(type return_type, type* parameter_types, constant? trailing_return)
     | IncompleteArrayType(type type)
     | InjectedClassNameType(type type)
     | LValueReferenceType(type type)
     | MemberPointerType(type cls, type type)
     | PackExpansionType(type type)
     | ParenType(type type)
     | PointerType(type type)
     | QualType(identifier? qualifiers, type type)
     | RValueReferenceType(type type)
     | RecordType(identifier name)
     | SubstTemplateTypeParmType(type type)
     | TemplateTypeParmType(identifier name)
     | TemplateSpecializationType(identifier name, template_argument* template_args)
     | TypedefType(identifier name, type type)
     | TypeOfExprType(constant repr)
     | UnresolvedUsingType(identifier name)
     | VariableArrayType(type type, constant size_repr)
     | VectorType(type type, constant size)

enum_field = EnumConstantDecl(identifier name, expression? init)

constrained_expression = ConstrainedExpression(expression expr, constant constraint)

attribute = AlignedAttr(expression? size)
          | AliasAttr(identifier aliasee)
          | AllocAlignAttr(constant index)
          | AllocSizeAttr(constant size, constant? nmemb)
          | AlwaysInlineAttr
          | AnyX86InterruptAttr
          | CXX11NoReturnAttr
          | CarriesDependencyAttr
          | CleanupAttr(identifier func)
          | ColdAttr
          | ConstAttr
          | ConstructorAttr(constant? priority)
          | DeprecatedAttr(constant? msg)
          | DestructorAttr(constant? priority)
          | ErrorAttr(constant msg)
          | FallThroughAttr
          | FlattenAttr
          | FormatAttr(identifier archetype, constant fmt_index, constant vargs_index)
          | FormatArgAttr(constant fmt_index)
          | GNUInlineAttr
          | HotAttr
          | IFuncAttr(identifier name)
          | LeafAttr
          | LikelyAttr
          | MallocAttr
          | NoInlineAttr
          | NoInstrumentFunctionAttr
          | NoProfileFunctionAttr
          | NoReturnAttr
          | NoSanitizeAttr(constant* options)
          | NoSplitStackAttr
          | NoStackProtectorAttr
          | NoUniqueAddressAttr
          | NonNullAttr(constant* indices)
          | PackedAttr
          | PatchableFunctionEntryAttr(constant count, constant? offset)
          | PureAttr
          | RetainAttr
          | ReturnsNonNullAttr
          | ReturnsTwiceAttr
          | SectionAttr(constant section)
          | SentinelAttr(constant? value, constant? offset)
          | TargetAttr(constant desc)
          | TargetClonesAttr(constant desc)
          | TLSModelAttr(constant tls_model)
          | UnavailableAttr(constant? msg)
          | UninitializedAttr
          | UnlikelyAttr
          | UnusedAttr
          | UsedAttr
          | VisibilityAttr(constant visibility)
          | WarnUnusedResultAttr
          | WeakAttr
          | WeakRefAttr(identifier? name)

base = Base(identifier name, access_spec? access_spec, virtual_spec? virtual)

access_spec = Public
            | Protected
            | Private

virtual_spec = Virtual

default_mode = Default
             | Delete
             | PureVirtual

method_attr = FinalAttr
            | OverrideAttr

exception_spec = Throw(identifier* args)
               | NoExcept(constant? repr)
               | NoThrow

offsetof_kind = OffsetOfField(identifier name)
              | OffsetOfArray(expression index)

exception_handler = CXXCatchStmt(declaration? decl, statement body)

auto_keyword = Auto | DecltypeAuto | GNUAutoType
